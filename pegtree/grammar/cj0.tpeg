/* CJ (Controlled Japanese) 文法 */

/**
* トップの規則
* 文は Chunk の繰り返しとみなす
*/

Sentence = {
    (Chunk)* EOS
    #CJ
}

EOS = [。．.\n]? EOF
EOF = !.

TEN = [、，,：　 \n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
HIRA = [ぁ-ん]
CMD = [！!]
K = KANJI/KATA
H = HIRA
C = KANJI/HIRA/KATA

Chunk =
  / {TEN #Ten }
  / Character
  // Dictionary
  / Pattern
  // Parenthesis _Nend?
  / { [。．.\n] #EOS}
  // Expression _ _Nend?
  / Unknown

Unknown = {
    (!K .)+
    #Unknown
}

/* Character */
// いる　一段動詞はパターンマッチできない
// な行は死ぬのみ

Character =
    / { CVERB1 #Verb1 } _Verb1
    / { [死し] #VN5 } _VerbNA5

CVERB1 = [鋳診観視見経簸着看獲煮流歴恐得干居射寝割似出ゐれみへひねにでてせきえうい] &__V1START

/* Pattern */

Pattern =
  / { H? K* #Noun } (_Kend / _HPattern) 

_HPattern = K / {^ H #Noun} (_Kend)

/* ひらがな */

_HPATTEN = _Kend

_Kend = 
  / _NendHA / _NendDA / _KendDE
  / _KendNN / _KendTTU 
  / _KendI / _KendU / _KendE / _KendO
  / _KendKA / _KendKI / _KendKU / _KendKE / _KendKO
  / _KendSA / _KendSI / _KendSU / _KendSE / _KendSO
  / _KendTA / _KendTI / _KendTU / _KendTE / _KendTO
  / _KendNA / _KendNI / _KendNU / _KendNE / _KendNO
  / _KendMA / _KendMI / _KendMU / _KendME / _KendMO
  / _KendRA / _KendRI / _KendRU / _KendRE / _KendRO
  / _KendGA / _KendGI / _KendGU / _KendGE / _KendGO
  / _KendBA / _KendBI / _KendBU / _KendBE / _KendBO
  / _KendZI / _KendZU / _KendZE 
  / _NendYA / _NendYU / _NendYO 

_Adj =
  / _AdjI  //赤い
  / _AdjKA //赤かった
  / _AdjKU //赤く
  / _AdjSA //赤さ
  / _AdjME //赤め

/* あ行 */

_KendI = 
    / {^ 'い' #Vt5 } _VerbT   // 書いた
    / {^ 'い' #VW5 } _VerbIn　//　笑い

_AdjI = {^ 'い' #Base} // 赤い

_KendU = {^ 'う' #VW5 } _VerbU  // 笑う
_KendE = {^ 'え' #VW5 } _VerbE  // 笑えば
_KendO = {^ 'お' #VW5 } _VerbO  // 笑おう

/* か行 */

_KendKA =
    / {^ 'か' #VK5 } _VerbA  // 書かない
    / _AdjKA
    / _VendKA
_AdjKA = {^ 'かった' #Past} _Pend?
_NendKA =
    / {^ 'かも' ('知れ'/'しれ') ('ない'/'ません'/'ず') #May}
    / {^ 'から' !_HPATTEN (_NO)? #Argument }
    / { 'か' (DOUKA / !KA_V !_HPATTEN) #Whether } _Nend?

_VendKA = _NendKA
DOUKA = TEN? ('どうか' / '否か') TEN?

_KendKI = {^ 'き' #VK5 } _VerbIn
_KendKU = 
  / {^ 'く' #PAdj } __KendKU
  / {^ 'く' #AVK5 } _VerbU // 書く, 赤く

_AdjKU = 
  / {^ 'く' #PAdj } __KendKU
  / {^ 'く' #A } _VerbU // 書く, 赤く

__KendKU = 
    / {^ 'な' #Not } _Adj // 赤くない
    / {^ 'な' #Become } _VerbRA5 // 赤くなる

_KendKE = {^ 'け' #VK5 } _VerbE
_KendKO = {^ 'こ' #VK5 } _VerbO

/* さ　*/

_KendSA =
    / {^ 'さ' #VS5 } _VerbA  // ない
    / _NendSA
    / _AdjSA

_NendSA =
    / {^ 'さ' #VS } {^ 'れ' #Been } _Verb1 
    / {^ 'さ' #VS } {^ 'せ' #Make } _Verb1

_AdjSA = {^ 'さ' ![れせ] #Noun } _Nend?

_KendSI =
    / {^ 'し' &__V1START #PVerb1 } _Verb1  
    / _AdjSI
    / _NendSI

_AdjSI = {^ 'し'  #PAdj} _Adj // 美味しい

_NendSI = // 例　したりし
    / {^ 'し' #VS } (_VerbA / _VerbT / _VerbI / 'よ' _VerbO)
    / {^ 'し' [よろ] #VS } {^ CMD? #Command } _Vend?
    / {^ 'し' #Noun } _Nend?

_KendSU = 
  / _NendSU
  / {^ 'す' #VS5 } _VerbU  // 達すと

_NendSU = 
  / {^ 'する' #VS } _VerbU  // サ変
  / {^ 'す' #VS } ('れ' _VerbE / _Should) 

_KendSE =
  / {^ 'せ' #VK5 } _VerbE
  / _NendSE

_NendSE = 
  / {^ 'せよ' #VS } {^ CMD? #Command } _Vend?
  / {^ 'せず' / 'せぬ' #Noun } _Nend?

_KendSO = {^ 'そ' #VK5 } _VerbO

/* たちつてと */

_KendTA = {^ 'た' #VT5 } _VerbA  // ない
_KendTI = {^ 'ち' #VT5 } _VerbIn
_KendTU = {^ 'つ' !'け' #VT5 } _VerbU  // つける
_KendTE = {^ 'て' #VT5 } _VerbE  // ば
_KendTO =
  / {^ 'と' #VT5 } _VerbO  // う
  / _VendTO

_NendTO =
  / {^ 'と' (_HA/_MO/!TO_V) #Argument }

_VendTO =
  / {^ IF #If } _EvenIf?
  / _NendTO

IF = 'とき' / '時' / '場合'
_EvenIf = {^ _MO #EvenIf }

/* なにぬねの　*/

_VerbNA5 = // 切
    / {^ 'ん' #VR5 } _VerbD // て
    / _KendNA
    / _KendNI
    / _KendNU
    / _KendNE
    / _KendNO

_KendNA =
    / {^ 'な' #VN5 } _VerbA  // 死なない
    / _NendNA

_NendNA = 
  / {^ 'な' #PAdj} _Adj
  / {^ 'な' !NA_V /*!(_Iw)*/ #AdjN} // 立派な

_KendNI =
  / {^ 'に' #VN5 } _VerbI  // ながら
  / _NendNI

_NendNI = 
  / {^ 'に' !TO_V (_HA/_MO)? #Argument }
_VendNI = _NendNI

_KendNU = {^ 'ぬ' #VN5 } _VerbU  // 
_KendNE = {^ 'ね' #VN5 } _VerbE  // ば
    
_KendNO =
  / {^ 'の' #VN5 } _VerbO  // う
  / _NendNO

_NendNO = 
  {^ 'の' !NO_V (_HA/_MO/_GA/'を')? #Argument }

_VendNO = { 'の' #That} _Nend

/* はひふへほ */

_NendHA = {^ 'は' !HA_V !_HPATTEN #Argument }
_NendHE = {^ 'へ' (_NO / !HE_V !_HPATTEN) #Argument }

/* まみむめも　*/

_KendMA = {^ 'ま' #VM5 } _VerbA  // ない
_KendMI = {^ 'み' #VM5 } _VerbIn // ながら
_KendMU = {^ 'む' #VM5 } _VerbU  // 
_KendME =
    / {^ 'め' #VM5 } _VerbE  // ば
    / _AdjME
_AdjME = {^ 'め' #Noun } _Nend? //　高め

_KendMO =
  / {^ 'も' #VM5 } _VerbO  // う
  / _NendMO

_NendMO =
  / {^ 'も' !MO_V !_HPATTEN #Argument }
_VendMO = _NendMO

/* やゆよ */
_NendYA = {^ 'や' !YA_V !_HPATTEN #Argument }   // や
_NendYU = {^ 'ゆえ' !_HPATTEN (_NI/_NO)? #Argument }   // ゆえ
_NendYO = {^ 'より' !_HPATTEN #Argument }   // より

/* らりすれろ */

_VerbRA5 = // 切
    / {^ 'っ' #VR5 } _VerbT // て
    / _KendRA
    / _KendRI
    / _KendRU
    / _KendRE
    / _KendRO

_KendRA =
    / {^ 'ら' #VR5 } _VerbA  // ない
    / _NendRA 

_NendRA = {^ 'らし' #Sound } _Adj 
_VendRA = _NendRA

_KendRI = {^ 'り' #VR5 } _VerbIn  // ながら
_KendRU = {^ 'る' #VR5 } _VerbU  // 
_KendRE = {^ 'れ' #VR5 } _VerbE  // ば
_KendRO = {^ 'ろ' #VR5 } _VerbO  // う

/* わ行 */

_KendWA = {^ 'わ' #VW5 } _VerbA  // ない
_NendWO = {^ 'を' !_HPATTEN #Argument }

/* がぎぐげご */

_KendGA =
    / {^ 'が' #VG5 } _VerbA  // 防がない
    / _NendGA

_NendGA = {^ 'が' !GA_V !_HPATTEN #Argument } // 防が
_VendGA = { 'が' !GA_V !_HPATTEN #Conjunction }

_KendGI = {^ 'ぎ' #VG5 } _VerbIn  // 防ぎ
_KendGU = {^ 'ぐ' #VG5 } _VerbU  // 防ぐ
_KendGE = {^ 'げ' #VG5 } _VerbE  // 防げ
_KendGO = {^ 'ご' #VG5 } _VerbO  // 防ごう

/* ざじずぜぞ*/
_KendZI =
    / {^ 'じ' #VZ } (_VerbA / _VerbT / _VerbI / 'よ' _VerbO)
    / {^ 'じさ' &'れ' #VZ } {'れ' #Been } _Verb1 
    / {^ 'じさ' &'せ' #VZ } {'せ' #Make } _Verb1
    / {^ 'じ' #Noun } _Nend?

_KendZU =
    / {^ 'ずる' #VZ } _VerbU  // サ変
    / {^ 'ず' #VZ } ('れ' _VerbE / _Should) 

_KendZE =
    / {^ 'ぜ' &__V1START #PVerb1 } _Verb1
    / {^ 'ぜよ' #VZ } {^ CMD? #Command } _Vend?
    / {^ 'ぜず' / 'ぜぬ' #Noun } _Nend?

/* ばびぶべぼ */

_KendBA = {^ 'ば' #VB5 } _VerbA  // 遊ばない
_KendBI = {^ 'び' #VB5 } _VerbIn 　// 遊び
_KendBU = {^ 'ぶ' #VB5 } _VerbU  // 遊ぶ
_KendBE = {^ 'べ' #VB5 } _VerbE  // 遊べ
_KendBO = {^ 'ぼ' #VB5 } _VerbO  // 遊ぼう

/* で */

_VendDA =
  / {^ 'だろう' #VB }　{^ #Would } _Vend?

_NendDA = 
  / _VendDA
  / {^ 'だった' #VB }　{^ #Past } _Pend?
  / {^ 'だ' !DA_V !_HPATTEN  #VB } _Vend? // だせる

_KendDE = // LIMIT 茹でたまご
  / {^ 'で' &__V1START #PVerb1 } _Verb1
  / _NendDE

_NendDE =
  / {^ 'でき' #Can } _Verb1
  / _VendDE  

_VendDE = 
  / {^ 'でした' #VB }　{^ #Past } _Pend?
  / {^ 'でしょう' #VB }　{^ #Would } _Vend? 
  / {^ 'です' #VB }　_Vend? // だせる _Vend?
  / {^ 'で' !DE_V !_HPATTEN OF #Argument }

/* 促音 */

_KendTTU = {^ 'っ' #Vt5 } _VerbT // て
_KendNN = {^ 'ん' #Vd5 } _VerbD // で

/* 活用 */

_Verb1 = // 過ぎ　述べ　視　でき
    / {^ 'る' #Base } _VerbU  // 
    / {^ 'れ' #Verb } _VerbE  // ば
    / {^ 'られ' #Been } _Verb1 // 述べられています
    / {^ 'よ' #Verb } _VerbO  // う
    / _VerbA  // ない
    / _VerbT // て
    / _VerbI　// て

_Verb1n = // 過ぎ　述べ　視　でき
    / _Verb1
    / {^ #Noun } (_Nend)?  // 

_VerbA = // 書か+ない   探さ+ない
    / {^ 'な' #Not } _Adj
    / {^ 'ず' #Noun } _Nend?
    / {^ 'さ'? 'せ' #Make } _Verb1
    / {^ 'れ' #Been } _Verb1 

_VerbI = // 書き+つつ  探し+つつ　
    / &'ま' _VerbIP
    / {^ 'た' #Want } _Adj   // 書きたい
    / {^ ('やす' / 'よ' ) #Easy } _Adj  // 書きやすい
    / {^ ('にく' / 'がた' / 'づら') #Hard } _Adj //書きがたい
    / {^ 'がち' #Noun } _Nend? //休みがちだ
    / {^ ('つつ' / 'ながら') #While }
    / {^ &__V1START #PVerb1 } _Verb1

_VerbIn = // 書き+つつ  探し+つつ　
    / _VerbI
    / {^ #Noun } (_Nend)?

_VerbIP =
    / {^ ('ます' / 'ましょう') #Base } _Polite _Vend?
    / {^ 'ません' #Not } _Polite _Vend?
    / {^ 'ましたら' #If } _Polite
    / {^ 'ました' #Past } _Polite _Pend?
    / {^ 'ましても' #EvenIf } _Polite
    / {^ 'まして' #And } _Polite

_Polite = {^ #Polite }

_VerbT = // 書い+て   探し+て　
    / {^ 'た' #Want } _Adj   // 特例. 記録したい
    / {^ 'た' /*!_TAHw*/ #Past } _Pend?  // !たてる
    / {^ 'て' #Base } _VerbKUDASAI

// たえる　たてる・ためる・たれる　たおすたたく
//_TAHw = [えおせただてめれ]  

_VerbD = // 遊んだ   遊んで
    / {^ 'だ' #Past } _Pend?
    / {^ 'で' #Base } _VerbKUDASAI

_VerbKUDASAI =
  / {^ KUDASAI #Command } {^ #Polite} _Vend?
  / {^ _MO #EvenIf }
  / _VerbIP 
  / {^ #Noun } (&NSTART / _Nend)  // 翔んで埼玉
  // {^ #And } _TryMood?

KUDASAI = 'ください'/'下さい'

_VerbU = &VEND _Vend? // 書く　探す

_VerbE = // 書け+ば  探せ+ば
    / {^ 'ば' #If }
    / {^ CMD #Command }
    / {^ #PVerb1 } _Verb1n

_VerbO = // 書こ+う　探そ+う
    / {^ 'う' #Will } _Vend?

_Should = 
    {^ ('べき' / 'べし' / 'はず' ) #Should } _Nend?

_Nend =
  / _NendHA // は
  / _NendGA // が
  / _NendMO // も
  / _NendDA // だ
  / _NendDE // で
  / _NendNI // に
  / _NendTO // と
  / _NendKA // か
  / _NendSA // 勤勉さ
  / _NendSI // 使用しろ
  / _NendSU // 使用す
  / _NendSE // 使用せよ
  / _NendRA // 彼らしい
  / _NendYA // や
  / _NendYU // ゆえ
  / _NendYO // よう

_Vend =
  / _VendKA // するか
  / _VendTO // すると
  / _VendDA // するだろう
  / _VendDE // するでしょう
  / _VendRA // するらしい
  / _VendGA // が
  // / {^ 'に' ('違い'/'ちがい') ('ない' / 'ありません') #Must } _Vend?
  // / {^ 'しかありません' #Only }
  // / { 'が' #Conjunction }
  // / { 'けれども' #Conjunction } // けれども
  // / { NSUFFIX #That } _Nend?
  // / { 'のみ'/ 'だけ' #That } _Nend?
  // / { 'ため' / '為' / 'から' #Because } _Nend?
  // / { 'こと' / 'の'　#That } _Nend?

_Pend =
  / {^ 'り' #Noun } (_Nend)
  / {^ 'な'? 'ら' 'ば'? #If } 
  / _Vend

// パターン

NSTART = K / EOF / &H NOUN 
VEND = K / EOF / &H (_HVCONJ / NOUN)
NEND = K / EOF / &H (_HNCONJ)

VERB =
    / 'したが'
    / 'がんばれ'

NOUN =
    / 'こと'
    / 'もの'
    / 'とき' / 'ところ'

NTAIL =
  / '置き' / 'おき' // 1日おき
  / 'ごと' 
  / 'さま' / 'さん' / 'ちゃん'
  / 'たち' 
  / 'ずくめ' / 'だらけ' / 'ばかり'
  / 'など' / 'がら'
  / 'さ' ![せれ]

_HNCONJ =
    / 'だ' ('った'/'ろ'/ ![けせ])
    / 'で' ('す'/'し')
    / 'な'　NSTART
    / NSUFFIX
    / NTAIL

_HVCONJ = // する　赤い
    / 'に' 
    / 'の' [ではもをだとに]
    / 'か' [らもど否と、，？]
    / 'で' [すし]
    / 'だ' [ろけ]
    / 'けれども' / 'けど'
    / 'ばかり'
    / 'らし'
    / 'どころ' 
    / 'うえ' / 'なか'
    / 'ものの'
    / 'ゆえに'

NSUFFIX = 
  / 'を'
  / 'は' !HA_V
  / 'が' !GA_V
  / 'も' !MO_V
  / 'や' !YA_V
  / 'に' ('ついて'/'関して'/'より'/ 'よって')? !NI_V　OF
  / 'と' !TO_V OF
  / 'で' !DE_V 'は'? OF
  / 'へ' !HE_V OF
  / 'の' !NO_V (('よう'/'とおり'/'通り') 'に'?)?
  / 'から' OF
  / 'より' OF
  / 'まで' OF
  / ('から' / 'かぎり') OF
  / 'か' ('どうか' / '否か' / [とのをに] / _MO / TEN ) OF
  / 'だけ' OF
  / 'のみ' OF
  / 'ばかり' OF
  / 'さえ' OF
  / 'しか'
  / 'こそ' OF
  / 'なら' (('ば'/'では') OF)?

OF = ('の' / _MO / 'を' / 'に')?

__V1START =  // 高めた 落ちる　(勝ちますと区別つかない)
    / [るたてま]
    / 'られ'
    / NAIw

NAIw = 'な' _Iw
_Iw = [いく] / 'かった' / 'ければ'

/* 助詞で始まるひらがな動詞 */

KA_V = /*か*/
    / _HSA5
    / [えざさ] // かえ　かざ　かさ

GA_V = /*が*/
    / 'え' // がえ

NA_V = /*な*/
    / _HRA5
    / [がお] //'なが' / 'なお'

NO_V = /*な*/
    / _HRA5
    / [ばけべび] // 'のば' / 'のけ' / 'のべ' / 'のび'

HA_V = /* は*/
    / _HRA5
    / [らやかなずだまさじたこねてめ]
    // / 'はら' / 'はや' / 'はから' / 'はな' / 'はず'
    // / 'はか' / 'はだか' / 'はま' / 'はさ' / 'はじめ'
    // / 'はたら' / 'はこ' / 'はね' / 'はて' / 'はめ' 

TO_V = /* と */
    / _HRA5 
    / [ばおどめ] // 'とば' / 'とお' / 'とど' / 'とどま' / 'とめ'

NI_V = /*に*/
    / __V1START
    / 'が' 

HE_V = /* へ */
    / _HRA5
    / [らつ] //'へら' / 'へつら'

YA_V = /* や */
    / (_HRA5 / _HKA5)
    / [ぶめ] //'やぶ' / 'やめ'

DA_V = /* だ　*/
    / _HSA5 / __V1START

DE_V =
    / __V1START
 
MO_V = /* も */
    / _HTA5 
    / [らとどれ] //'もら' / 'もど' / 'もとめ' / 'もれ'

_HKA5 = [かきいくけこ]
_HSA5 = [さしすせそ]
_HTA5 = [たちっつてと]
_HRA5 = [らりっるれろ]

_HA = 'は'
_NO = 'の'
_MO = !('もっと'/'もう'/'もし') 'も'
_NI = 'に'
_GA = 'が'

