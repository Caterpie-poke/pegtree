/* CJ (Controlled Japanese) 文法 */

/**
* トップの規則
* 文は Chunk の繰り返しとみなす
*/

Sentence = {
    (Chunk _?)* EOS
    #CJ
}

EOS = [。．.\n]? EOF
EOF = !.

_ = [　 ]*
TEN = [、，,：]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
HIRA = [ぁ-ん]
ALNUM = [a-zA-Z0-9]
JALNUM = [ａ-ｚＡ-Ｚ０-９]
CMD = [！!]

K = KANJI/KATA/JALNUM
H = HIRA
C = KANJI/HIRA/KATA

Chunk =
  / {TEN #CT }
  / Parenthesis
  / Number / Symbol
  / Dictionary
  / Onomatopoeia
  / Character
  / Pattern
  / { [。．.\n] #CEOS }
  // Expression _ _Nend?
  / Unknown


/* Character */
// いる　一段動詞はパターンマッチできない
// な行は死ぬのみ

Dictionary = 
  / { ADVERB #NM } _Nend
  / { VERB #V } _Kend
  / { ADJ #A } _Adj
  / { NOUN #N } _Nend

/* Character */
// いる　一段動詞はパターンマッチできない
// な行は死ぬのみ

Character =
    / { CVERB1 &_HV1 #V1 } _Verb1
    / { [死し] #VN5 } _VerbNA5

CVERB1 = [鋳診観視見経簸着看獲煮流歴恐得干居射寝割似出ゐれみへひねにでてせきえうい]
//CNA = [恣稀別希妙歪素朧更嫌縦罪酷厭密徒闌端酣初俄切邪変乙主雑暇露粋艷楽急俗顕や雅生純義円重]



/* Onomatopoeia オノマトペ語彙　*/

Onomatopoeia = &H _Onomatopoeia _Kend?

_Onomatopoeia = 
  / { @scope(@symbol(PAT2w) @match(PAT2w)) #NM }
  / { @scope(@symbol(PAT3w) @match(PAT3w)) #NM }

PAT2w = H C
PAT3w = H C C

/* Pattern */

Pattern = { (H K* / K+) #N } (_Ten / _Kend / _HPattern) 

_Ten = {^ TEN #NM }

_HPattern = !H / &NSTART / {^ H #N. } ( _Kend / _HPattern)

/* ひらがな */

_Kend = &H _KendH

_KendH = 
  / _NendHA / _NendWO / _NendDA / _KendDE
  / _KendNN / _KendTTU 
  / _KendI / _KendU / _KendE / _KendO
  / _KendKA / _KendKI / _KendKU / _KendKE / _KendKO
  / _KendSA / _KendSI / _KendSU / _KendSE / _KendSO
  / _KendTA / _KendTI / _KendTU / _KendTE / _KendTO
  / _KendNA / _KendNI / _KendNU / _KendNE / _KendNO
  / _KendMA / _KendMI / _KendMU / _KendME / _KendMO
  / _KendRA / _KendRI / _KendRU / _KendRE / _KendRO
  / _KendGA / _KendGI / _KendGU / _KendGE / _KendGO
  / _KendBA / _KendBI / _KendBU / _KendBE / _KendBO
  / _KendZI / _KendZU / _KendZE 
  / _NendYA / _NendYU / _NendYO 

_Adj =
  / _AdjI  //赤い
  / _AdjKA //赤かった
  / _AdjKU //赤く
  / _AdjSA //赤さ
  / _AdjME //赤め

_Nai =
  / {^ 'い' } _Vend
  / {^ 'く' #Xthen } __KendKU?
  / {^ 'かった' #Xpast } _Pend
  / _AdjSA

/* あ行 */

_VerbWA5 =
  / {^ 'っ' #VW5 } _VerbT　//　笑い
  / {^ 'い' #VW5 } _VerbI　//　笑い
  / _KendWA
  / _KendU
  / _KendE
  / _KendO

_KendI = 
    / {^ 'い' #Vt5 } _VerbT   // 書いた
    / {^ 'い' #VW5 } _VerbI　//　笑い
    / {^ 'い' #A } _Vend
    / {^ 'い' #NA. } _Nend // 赤い @NA 形容詞かも知れない

_AdjI = {^ 'い' #A } _Vend // 赤い

_KendU = {^ 'う' #VW5 } _VerbU  // 笑う
_KendE = {^ 'え' #VW5 } _VerbE  // 笑えば
_KendO = {^ 'お' #VW5 } _VerbO  // 笑おう

/* か行 */

_VerbKA5 =
  / {^ 'か' #VK5 } _VerbA  // 書かない
  / {^ 'い' #VK5 } _VerbT   // 書いた
  / {^ 'く' #VK5 } _VerbU // 書く, 赤く
  / _KendKI
  / _KendKE
  / _KendKO

_KendKA =
    / {^ 'か' #VK5 } _VerbA  // 書かない
    / _AdjKA
    / _May {^ #Xbe }
    / !('か' (KA_V/_HNEXT)) _NendKA

_AdjKA = {^ 'かった' #AXpast} _Pend?
_NendKA =
    / {^ 'から' (HA/_MO/_NO/!_HNEXT) #X }　　// 取りはから+う
    / { 'か' (TEN DOUKA? / DOUKA ) #CXwhether } _Nend

_May = {^ 'かも' ('知れ'/'しれ')? ('ない'/'ません'/'ず')? #Xmay}

_VendKA = _NendKA
DOUKA = ('どうか' / '否か') TEN?

_KendKI = {^ 'き' #VK5 } _VerbIn
_KendKU = 
  / {^ 'くさ' #A. } _Adj // 蒼くさい
  / {^ 'く' #A. } _VerbDo
  / {^ 'く' #AXthen } __KendKU
  / {^ 'く' #AVK5Xthen } _VerbU // 書く, 赤く

_AdjKU = 
  / {^ 'くさ' #A. } _Adj
  / {^ 'く' #A. } _VerbDo
  / {^ 'く' #AXthen } __KendKU
  / {^ 'く' #AXthen } _VerbU // 書く, 赤く

__KendKU = 
    / {^ 'な' #Xnot } _Nai // 赤くない
    / { 'な' #VR5 } _VerbRA5 // 赤くなる

_KendKE = {^ 'け' #VK5 } _VerbE
_KendKO = {^ 'こ' #VK5 } _VerbO

/* さ　*/

_KendSA =
    / _NendSA
    / {^ 'さ' #VS5 } _VerbA  // ない
    / _AdjSA

_NendSA =
  / {^ 'され' #VSXpassive } _Verb1 
  / {^ 'させ' #VSXmake } _Verb1

_AdjSA = 
  / {^ 'さ' ![れせ] #N. } _Nend

_KendSI =
    / _AdjSI
    / _NendSI
    / {^ 'し' &_HV1 #V1. } _Verb1  

_AdjSI = {^ 'し'  #A. } _Adj // 美味しい

_NendSI = // 例　したりし
    / {^ 'し' #VS } (_VerbA / _VerbT / _VerbI / 'よ' _VerbO)
    / {^ 'し' [よろ] #VS } {^ CMD? #Xdo } _VendH
    / {^ 'し' #N. } _Nend

_KendSU = 
  / _NendSU
  / {^ 'す' #VS5 } _VerbU  // 達すと

_NendSU = 
  / {^ 'する' #VS } _VerbU  // サ変
  / {^ 'すれ' #VS } _VerbE 

_KendSE =
  / _NendSE
  / {^ 'せ' #VS5 } _VerbE

_NendSE = 
  / {^ 'せよ' #VS } {^ CMD? #Xdo } _Vend
  / {^ ('せず' / 'せぬ') #NXnot. } _Nend

_KendSO = {^ 'そ' #VS5 } _VerbO

_VerbDo = 
  / {^ 'する' #VSXset } _Vend
  / {^ 'した' #VSXsetXpast } _Pend
  / {^ 'しな' #VSXsetXnot } _Nai
  / {^ 'すれば' TEN? #VSXsetXif }
  / {^ 'し' 'て'? #VSXsetXthen }  //AをBとして、CをDとする


/* たちつてと */

_KendTA = {^ 'た' #VT5 } _VerbA  // ない
_KendTI = {^ 'ち' #VT5 } _VerbIn
_KendTU = {^ 'つ' #VT5 } _VerbU  // つける
_KendTE = {^ 'て' #VT5 } _VerbE  // ば
_KendTO =
  / {^ 'と' #VT5 } _VerbO  // う
  / _NendTO

_NendTO =
  / {^ 'と' HA #X } {^ #Xdefine }
  / {^ 'と' #NM. } _VerbDo
  / {^ 'と' (_TOSUFFIX (HA / _MO / _NO)? / _TORESERVED / HA / _MO / _NO ) #X }

_TOSUFFIX =
  / 'ともに'
  / 'として'

_VendTO =
  / { IF #NCXif } (TEN/_EvenIf/_Nend)
  / { 'ところ' #NC } (TEN / _Nend)
  / { 'と' (_TOSUFFIX) #NC } ( TEN / _Nend)

IF = 'とき' / '時' / '場合'
_EvenIf = {^ _MO #Xeven }

/* なにぬねの　*/

_VerbNA5 = // 切
    / {^ 'ん' #VR5 } _VerbD // て
    / _KendNA
    / _KendNI
    / _KendNU
    / _KendNE
    / _KendNO

_KendNA =
    / {^ 'な' #VN5 } _VerbA  // 死なない
    / _NendNA

_NendNA = 
  / {^ 'な' #AXnot. } _Adj
  / {^ 'な' &NSTART !NA_V  #AN. } // 立派な

_KendNI =
  / {^ 'に' #VN5 } _VerbI  // ながら
  / _NendNI

_NendNI = 
  / {^ 'に' #NM. } _VerbDo
  / {^ 'に' (_NISUFFIX (HA/_MO)? / (HA/_MO) / !NI_V !_HNEXT) #X }

_NISUFFIX = 
  / 'ついて' / '対して' / 'とって'
  / '関して' 
  / '加えて'
  / 'より' / 'よると' / 'よれば'
  / '基づいて' / 'もとづいて'
  / 'よって'
  / '限り' / 'かぎり'

_VendNI =
  / {^ 'に' (HA/_MO) #X}
  / {^ 'に' #NM. }

_KendNU = {^ 'ぬ' #VN5 } _VerbU  // 
_KendNE = {^ 'ね' #VN5 } _VerbE  // ば
    
_KendNO =
  / {^ 'の' #VN5 } _VerbO  // う
  / _NendNO

_NendNO = 
  / { 'のみ' #NSXonly } _Nend
  / {^ 'の' !NO_V !_HNEXT (HA/_MO/_GA/'を')? #X }

_VendNO = 
  / { 'のみ' #CXonly } _Nend
  / { 'の' #CXthat } _Nend

/* はひふへほ */

_NendHA = {^ 'は' !HA_V #X }
_NendHE = {^ 'へ' (_NO / !HE_V　) #X }

/* まみむめも　*/

_KendMA = 
  / {^ 'ま' #VM5 } _VerbA  // ない
  / _NendMA

_NendMA = 
  / { 'または' TEN? #CXor }
  / {^ 'まで' (HA/_MO/_NO/_NI/_GA/_TO/'を')? #X }

_KendMI = {^ 'み' #VM5 } _VerbIn // ながら
_KendMU = {^ 'む' #VM5 } _VerbU  // 
_KendME =
    / {^ 'め' #VM5 } _VerbE  // ば
    / _AdjME
_AdjME = {^ 'め' #NXadj. } _Nend //　高め

_KendMO =
  / {^ 'も' #VM5 } _VerbO  // う
  / _NendMO

_NendMO = {^ 'も' !MO_V !_HNEXT #X }
_VendMO =
  / &MOSTART
  / { 'も' #CXeven }

MOSTART = 'も' [うしのっとち]

/* やゆよ */
_NendYA = {^ 'や' !YA_V !_HNEXT #X }   // や
_NendYU = {^ 'ゆえ' (_NI/_NO)? #X }   // ゆえ
_NendYO = {^ 'より' #Xthan }   // より

/* らりすれろ */

_VerbRA5 = // 切
    / {^ 'っ' #VR5 } _VerbT // て
    / _KendRA
    / _KendRI
    / _KendRU
    / _KendRE
    / _KendRO

_KendRA =
    / {^ 'ら' #VR5 } _VerbA  // ない
    / _NendRA 

_NendRA = {^ 'らし' #A. } _Adj 
_VendRA = _NendRA

_KendRI = {^ 'り' #VR5 } _VerbIn  // ながら
_KendRU = {^ 'る' #VR5 } _VerbU  // 
_KendRE = {^ 'れ' #VR5 } _VerbE  // ば
_KendRO = {^ 'ろ' #VR5 } _VerbO  // う

/* わ行 */

_KendWA = {^ 'わ' #VW5 } _VerbA  // ない

_NendWO = {^ 'を'  #X }

/* がぎぐげご */

_KendGA =
    / {^ 'が' #VG5 } _VerbA  // 防がない
    / _NendGA

_NendGA = {^ 'が' !GA_V !_HNEXT #X } // 防が
_VendGA = { 'が' !GA_V #C }

_KendGI = {^ 'ぎ' #VG5 } _VerbIn  // 防ぎ
_KendGU = {^ 'ぐ' #VG5 } _VerbU  // 防ぐ
_KendGE = {^ 'げ' #VG5 } _VerbE  // 防げ
_KendGO = {^ 'ご' #VG5 } _VerbO  // 防ごう

/* ざじずぜぞ*/
_KendZI =
  / _AdjZI
  / {^ 'じ' #VZ } (_VerbA / _VerbT / _VerbI / 'よ' _VerbO)
  / {^ 'じら' #VZXpassive } _Verb1 
  / {^ 'じさせ' #VZXmake } _Verb1
  / {^ 'じ' #N. } _Nend

_AdjZI = {^ 'じ'  #A. } _Adj // 美味しい

_KendZU =
    / {^ 'ずる' #VZ } _VerbU  // サ変
    / {^ 'ず' #VZ } ('れ' _VerbE) 

_KendZE =
    / {^ 'ぜ' &_HV1 #V1. } _Verb1
    / {^ 'ぜよ' #VZXdo } _Vend
    / {^ ('ぜず' / 'ぜぬ') #NXnot. } _Nend

/* ばびぶべぼ */

_KendBA = {^ 'ば' #VB5 } _VerbA  // 遊ばない
_KendBI = {^ 'び' #VB5 } _VerbIn 　// 遊び
_KendBU = {^ 'ぶ' #VB5 } _VerbU  // 遊ぶ
_KendBE = {^ 'べ' #VB5 } _VerbE  // 遊べ
_KendBO = {^ 'ぼ' #VB5 } _VerbO  // 遊ぼう

/* で */

_VendDA =
  / {^ 'だろう' #Xwould } _Vend

_NendDA = 
  / {^ 'だろう' #XbeXwould } _Vend
  / {^ 'だった' #XbeXpast }　_Pend
  / {^ 'だ' !DA_V #Xbe } _Vend // だせる

_KendDE = // LIMIT 茹でたまご
  / {^ 'で' !'な' &_HV1 #V1. } _Verb1
  / _NendDE

_NendDE =
  / {^ 'でき' #Xcan } _Verb1
  / {^ 'でした' #XbeXpast } _Pend?
  / {^ 'でしょう' #XbeXwould } _Vend 
  / {^ 'でな' #XbeXnot }　_Nai 
  / {^ 'で' #X } {'あ' #Xbe } _VerbRA5
  / {^ 'です' #Xbe }　_Vend 
  / {^ 'で' (HA/_MO/!DE_V) #X }

_VendDE = 
  / {^ 'でしょう' #Xwould } _Vend
  / 'です'　_Vend 

//SUNOUN = 'すべて' / 'すっ' / 'すこやか'

/* 促音 */

_KendTTU = {^ 'っ' #Vt5 } _VerbT // て
_KendNN = {^ 'ん' #Vd5 } _VerbD // で

/* 活用 */

_Verb1 = // 過ぎ　述べ　視　でき
    / {^ 'る' } _VerbU  // 
    / {^ 'れ' } _VerbE  // ば
    / {^ 'られ' #Xpassive } _Verb1 // 述べられています
    / {^ 'よ' } _VerbO  // う
    // 'な' {^ #AV1Xnot } _Nai // 情けない
    / _VerbA  // ない
    / _VerbT // て
    / _VerbI0　// て  再帰なし

_Verb1n = // 過ぎ　述べ　視　でき
    / _Verb1
    / {^ #N. } _Nend  // 

_VerbA = // 書か+ない   探さ+ない　// 見透かさせる
    / {^ 'な' #Xnot } _Nai
    / {^ 'さ'? 'せ' #V1Xmake. } _Verb1
    / {^ 'れ' #Xpassive } _Verb1 
    / {^ 'ず' #NXnot. } _Nend

_VerbI0 = // 書き+つつ  探し+つつ　再帰なし
    / _VerbIP
    / {^ 'た' #Xwant } _Adj   // 書きたい
    / {^ ('やす' / 'よ' ) #Xeasy } _Adj  // 書きやすい
    / {^ ('にく' / 'がた' / 'づら') #Xhard } _Adj //書きがたい
    / {^ 'がち' #N. } _Nend //休みがちだ
    / {^ ('つつ' / 'ながら') &CSTART #Xwhile }

_VerbI = // 書き+つつ  探し+つつ　
    / _VerbI0
    / {^ &_HV1 #V1. } _Verb1

_VerbIn = // 書き+つつ  探し+つつ　
    / _VerbI
    / {^ &(NSTART/VSTART/!_HNEXT) #NV. } _Nend    // 大き+い など

_VerbIP = 'ま' __VerbIP
__VerbIP = 
    / {^ ('す' / 'しょう') #Xpolite } _Vend
    / {^ 'せん' #XnotXpolite } _Vend
    / {^ 'したら' #XifXplite } 
    / {^ 'した' #XpastXplite } _Pend
    / {^ 'しても' #XevenXplite } 
    / {^ 'して' #XandXpolite }

_VerbT = // 書い+て   探し+て　
    / {^ 'た' #Xwant } _Adj   // 特例. 記録したい
    / {^ 'た' !TA_V #Xpast } _Pend?  // !たてる
    / {^ 'て' #Xthen } __VerbTD

_VerbD = // 遊んだ   遊んで
    / {^ 'だ' #Xpast } _Pend?
    / {^ 'で' #Xthen } __VerbTD

__VerbTD =
  / {^ KUDASAI #XdoXpolite } _Vend
  / {^ _MO #Xeven }
  / _VerbIP 
  / {^ 'あ' #Xnow } _Verb1 // 笑ってある
  / {^ 'な' #Xnot } _Adj // 笑ってある
  / {^ 'み' #Xtry } _Verb1 // 笑ってみる
  / {^ 'みせ' #Xtry } _Verb1 // 笑ってみる
  / {^ 'お' #Xdone } _VerbKA5    // 笑っておく
  / {^ 'い' #Xnow } _Verb1 // 笑っている, 
  / {^ [いゆ] #Xgoing } _VerbKA5  // 増加していく
  / {^ 'や' #X } _VerbRA5    // 笑ってやる
  / {^ 'くれ' #X } _Verb1    // 笑ってくれる
  / {^ 'もら' #Xget } _VerbWA5    // 笑ってもらう
  / {^ 'しま' #X } _VerbWA5    // 笑ってしまう
  / {^ 'ほし' #Xwant } _Adj    // 笑ってほしい
  / {^ #Xnow } _Verb1 // 笑ってる (口語)
  / {^ #NV. } (&CSTART / _Nend)  // 翔んで埼玉
  / {^ #Xthen }

//飲んで いらっしゃる
//励まして くださる
//これで ございます
//応援して いただく
//運んで あげる
//送って さしあげる

KUDASAI = 'ください'/'下さい'

_VerbU = _Vend // 書く　探す

_VerbE = // 書け+ば  探せ+ば
    / {^ 'ば' !_HNEXT #Xif }
    / {^ CMD #Xdo }
    / {^ #V1Xcan. } _Verb1n

_VerbO = // 書こ+う　探そ+う
    / {^ 'う' /*!_HNEXT*/ #Xletus } _Vend

// _Should = 
//     {^ ('べき' / 'べし' / 'はず' ) #Xshould } _Nend?

_Nend = EOS / &H _NendH / {^ TEN #NM } / NSTART
_NendH =
  / _NendDA // だ
  / _NendDE // で
  / _NendGA // が
  / _NendHA // は
  / _NendHE
  / _NendKA // か
  / _NendMA // または、まで
  / _NendMO // も
  / _NendNA // に
  / _NendNI // に
  / _NendNO // に
  / _NendRA // 彼らしい
  / _NendSA // 勤勉さ
  / _NendSI // 使用しろ
  / _NendSU // 使用す
  / _NendSE // 使用せよ
  / _NendTO // と
  / _NendWO // を
  / _NendYA // や
  / _NendYU // ゆえ
  / _NendYO // よう


_Vend = EOS / (&H _VendH) / NSTART
//_Vtest = {^ &[はがをにもですせ] #Q } _Nend
_VendH =
  / _VendDA // するだろう
  / _VendDE // するでしょう
  / _VendGA // が
  / _VendKA // するか
  / _VendMO
  / _VendNI
  / _VendNO
  / _VendTO // すると
  / _VendRA // するらしい
  / {^ 'べき' #Xshould } (NSTART / _Nend )
  // / {^ 'に' ('違い'/'ちがい') ('ない' / 'ありません') #Must } _Vend?
  // / {^ 'しかありません' #Only }
  // / { 'が' #Conjunction }
  // / { 'けれども' #Conjunction } // けれども
  // / { NSUFFIX #That } _Nend?
  // / { 'のみ'/ 'だけ' #That } _Nend?
  // / { 'ため' / '為' / 'から' #Because } _Nend?
  // / { 'こと' / 'の'　#That } _Nend?

_Pend =
  / {^ 'り' #N. } _Nend
  / { ('ら' / 'なら') 'ば'? #CXif } 
  / _Vend

// パターン

NSTART = !H / EOF / &H NOUN 
CSTART = !H / EOF / &H (NOUN / VSTART)

VERB =
    / 'したが'
    / 'がんばれ'

NOUN =
  / 'さまざま' / 'おだやか' / 'おおまか' / 'いっしょ' / 'あいまい' / 'ひらがな'
  / '改ざん' / '大まか' / 'お互い'
  / 'うしろ' / 'ところ'
  / 'こと' / 'もと' / 'もの' / 'とき' / 'ほか'
  / 'わけ' / 'せい' / 'ここ' / 'うえ'
  / 'ため' / 'なか' / 'あと'

ADVERB =
  / 'たとえば' / 'いたずら'
  / 'すべて'
  / '絶えず' / '決して' / '比較的' / '概して'
  / '極めて' / '更なる' / '後ほど' / '少しの'
  / '小さな' / '大した' / '多くの' / '同じく'
  / '単なる' / '初めて'
  / '例えば' / '何にも' / '互いに' / '二度と'
  / 'わずか' / 'よって' / 'やはり' / 'もはや' / 'もっと'
  / 'もしも' / 'むしろ' / 'まるで' / 'または' / 'ほんの'
  / 'どんな' / 'とても' / 'つまり' / 'ついで' / 'たとえ'
  / 'たった' / 'ただし' / 'そんな' / 'その後' /'その分'
  / 'そして' / 'そこで' / 'しかも' / 'しかし' / 'こんな'
  / 'かなり' / 'および' / 'およそ' / 'おなじ' / 'いつも'
  / 'いずれ' / 'あんな' / 'あまり'
  / '例え' / '再び' / '多少' / '少し' / '必ず' / '最も'
  / 'わが' / 'より' / 'よく' / 'やや' / 'もう' / 'まだ' / 'まず' / 'ほぼ'
  / 'なぜ' / 'なお' / 'どの' / 'ただ' / 'その' / 'そう' / 'すぐ' / 'さも'
  / 'ごく' / 'この' / 'こう' / 'かつ' / 'いつ' / 'あの' 
  / '通常' / '複数'

ADJ =
  / '少な' / 'たりな'


NTAIL =
  / '置き' / 'おき' // 1日おき
  / 'ごと' / 'びと'
  / 'さま' / 'さん' / 'ちゃん'
  / 'たち' 
  / 'ずくめ' / 'だらけ' / 'ばかり'
  / 'など' / 'がら'

VSTART =  // '学び' なおす
  / 'なお' / 'とれ' / 'と' _HRA5 / 'とめ' / 'とけ' / 'とどま'
  / 'あわ' / 'あげ' / 'つけ' / 'あが' 
  / 'まとめ' / 'おと' / 'もとめ' / 'すご' / 'すぎ'
  / 'かえ' / 'かれ' / 'くだ'
  / 'はな' / 'はか'


OF = ('の' / _MO / 'を' / 'に')?

_HV1 =  // 高めた 落ちる　(勝ちますと区別つかない)
    / [るたて]
    / 'ま' [すしせ]
    / 'られ'
    / NAIw

NAIw = 'な' _Iw
_Iw = [いく] / 'かった' / 'ければ'

/* 助詞で始まるひらがな動詞 */

KA_V = /*か*/
    / _HSA5
    / [えざさせ] // かえ　かざ　かさ かせる

GA_V = /*が*/
    / 'え' // がえ

NA_V = /*な*/
    / _HRA5 
    / [がお] //'なが' / 'なお'

NO_V = /*な*/
    / _HRA5
    / [ばけべび] // 'のば' / 'のけ' / 'のべ' / 'のび'

HA_V = /* は*/
    / _HRA5
    / [らやかなずだまさじたこねてめ]
    // / 'はら' / 'はや' / 'はから' / 'はな' / 'はず'
    // / 'はか' / 'はだか' / 'はま' / 'はさ' / 'はじめ'
    // / 'はたら' / 'はこ' / 'はね' / 'はて' / 'はめ' 

TA_V = [えおせただてめれ]
// たえる　たてる・ためる・たれる　たおすたたく
//_TAHw = [えおせただてめれ]  

// TO_V = /* と */
//     / _HRA5 
//     / [おけめばど] // 'とば' / 'とお' / 'とど' / 'とどま' / 'とめ' / 'とけ'
_TORESERVED　= &RESERVED / !_TOH
_TOH = // []
     / (_HRA5) / 'せ' (_HV1) / 'れ' (_HV1) / 'げ' (_HV1) / 'け' (_HV1) / 'め' (_HV1) / 'え' (_HV1) / 'び' (_HV1) / 'べ' (_HV1) / 'ほ' (_HGA5) / 'つ' (_HKA5) / 'づ' (_HKA5) / 'か' (_HKA5) / 'ど' (HA/_HKA5) / 'な' (HA/_HRA5/_HWA5/_HMA5) / 'お' (HA/_HRA5/_HSA5) / 'ま' (_HRA5) / 'た' (_HRA5) / 'わ' (_HRA5) / 'ば' (_HSA5) / 'の' (_HWA5) / 'し' (HA)
     / 'けな'/'なび'/'もな'/'なし'/'ずさ'/'がめ'/'らし'/'おし'/'げし'/'うし'/'くさ'/'ばせ'/'づけ'/'たり'/'のえ'/'らえ'/'よせ'/'ぼけ'/'おせ'/'ろえ'/'りご'/'きれ'/'づか'/'ずれ'/'なれ'/'まし'/'われ'/'しめ'/'どま'/'どけ'/'ばし'/'わし'

RESERVED = 'きれい'

NI_V = /*に*/
    / _HV1
    / 'が' 

HE_V = /* へ */
    / _HRA5
    / [らつ] //'へら' / 'へつら'

YA_V = /* や */
    / (_HRA5 / _HKA5)
    / [ぶめ] //'やぶ' / 'やめ'

DA_V = /* だ　*/
    / _HSA5 / _HV1

DE_V =
    / _HV1
 
MO_V = /* も */
    / _HTA5 
    / [らとどれ] //'もら' / 'もど' / 'もとめ' / 'もれ'

_HNEXT =
  / ADVERB 
  / _HKA5/_HSA5/_HTA5/_HMA5/_HRA5/_HWA5/_HGA5/_HBA5

_HKA5 = [かきいくけ] //こ]
_HSA5 = [さしすせ] //そ]
_HTA5 = [たちっつて] //と]
_HMA5 = [まみんむめ] //も]
_HRA5 = [らりっるれ] //ろ]
_HWA5 = [わいっうえ] //お]  // それも　お見合い
_HGA5 = [がぎいぐげ] //ご]
_HBA5 = [ばびいぶべ] //ぼ]
_HA = [いく] / 'かった' / 'ければ'

HA = 'は'
_NO = 'の'
_MO = !('も' ('っと'/[うしの])) 'も' 
_NI = 'に'
_GA = 'が'
_TO = 'と'

/* Symbols */

Parenthesis = 
  / POPEN _Parenthesis _Nend?
  / { '「' (!'」' .)+ '」' #NS } _Nend?
  / '『' {  (!'』' .)+  #N } '』' _Nend?
  / '[[' {  (!']]' .)+  #NL } ']]' _Nend?
  / '$' {  (!'$' !C .)+ #NM } '$' _Nend?
  /  { '\'' (!'\'' !C .)+ '\'' #NQ } _Nend?

_Parenthesis =　
  / { H+ #NR } PCLOSE // ルビー
  / { KATA+ #NR } PCLOSE // ルビー
  / { (ALNUM/' ')+ #NE } PCLOSE // 英訳
  / { (!PCLOSE . )+ #NP } PCLOSE //

POPEN = [(（《]
PCLOSE = [)）》]

/* Symbol */

Symbol = { [A-Za-z_] [A-Za-z0-9_.]* #NI } _Nend?

/* Number */

D = [.,0-9０-９]

Number = &D _Number _Nend?
_Number = 
  / { (D+ _ '年' _) (D+ _ '月' _)? (D+ _ '日')? #NDate }
  / { (D+ _ '月' _) (D+ _ '日')? #NDate }
  / { (D+ _ '時' _) (D+ _ '分')? #NTime }
  / { D+ #NN } {^ UNIT #NU}? 

UNIT = KATA+

Unknown = {
    (!K .)*
    #Unknown
}
