/* CJ Controlled Japanese 文法 */

/**
* トップの規則
* 文は Chunk の繰り返しとみなす
*/

Sentence = {
    (Chunk IGNORED?)* EOS
    #Sentense
}

IGNORED = ('ください' / 'です' / 'でした')

Chunk =
  // Adword       // 接続詞,副詞,連体詞
  / {TEN #Ten }
  / Character
  / Pattern1
  / Pattern
  / Unknown

/* Character */

Character =
    / { CVERB1 #Verb1 } _Verb1
    / { CADJ #Adj} _Adjective

CVERB1 = [鋳診観視見経簸着看獲煮流歴恐得干居射寝割似出ゐれみへひねにでてせきえうい] &_PVERB1w
CADJ = [黒高青難長鋭鈍重醜酸酷遠遍遅速近辛軽赤賢貴豪角薄蒼著荒苦若良臭腥脆聰聡羨繁緩細紅粘粗篤穢稚硬短眩眠白痛痒疎異畏甘狭狡熱煩煙無濃潔温渋清深淡浅洽汚永暗暑普易明早旨旧敏拙懶懈憎憂惨悪恥恐怪怖快強弱弛広幽幼少尊寒安好太多堆堅固善吝古厚凄円儚偉佳低乏丸イよぽなすこくえうい]

/* Pattern */

K = KANJI/KATA
H = HIRA
C = KANJI/HIRA/KATA
HEAD = (K+/H K*)

// 書く　赤く が区別できない
Pattern =
    / { HEAD (!PVERBw H)* #Noun } _Verb
    / { HEAD (!PADJw H)* #PAdj } _Adjective
    / { HEAD (!PADJc H)* #PAdj } _Adjective
    / { HEAD (!PNOUN H)* #PNoun } (_NounVerb/_Noun?)

Pattern1 =
    / { K+ #PNoun } (_NounVerb/_Noun)
    / { HEAD (!PVERBw H)? #Noun } _Verb
    / { HEAD (!PADJw H)? #Adj } _Adjective
    / { HEAD (!PNOUN H)? #Noun } (_NounVerb/_Noun)

PADJw = 
    / 'い' !_PVERBT _PSURU
    / 'く' ('て' / PNAIw / KANJI )
    / 'さ' ( PNOUNc / PNOUNw / EOF )
    / 'め' ( PNOUNc / PNOUNw / PVERB1w / EOF )
    / 'そう'
    / 'すぎ'
    / '過ぎ'

_PSURU = PSURUc / PSURUw / KANJI / EOF

PADJc = [さげめくい]

PNOUN = PNOUNVERBw / PNOUNw / PNOUNc

PNOUNVERBw =
  / 'し' (PNAIw / 'ま' / 'て' / 'ろ')
  / 'する'
  / 'すれば'

PNOUNw =
    / 'で' ('す' / 'した' / 'しょう')
    / 'だ' ('った' / 'ろう' / 'け'　)
    / 'に' ('ついて' / 'とって')
    / 'より'
    / 'まで'
    / 'へ' ('の')
    / 'か' ('どうか' / '否か' / 'ら' / 'ぎり')
    / 'のみ'
    / 'ばかり'
    / 'さえ'
    / 'し' ('か' / PNAIw)
    / 'する'
    / 'こそ'
    // {^ {'や' / 'および' / '及び'} #AndOr }
    // {^ {'または'} #Or }

PNOUNc = [はがもをのなでとに]

NOUNX = 
    / 'ごと'
    / 'ずつ'
    / 'さ' ![せれ]

PSURUw = // 赤い
    / 'な' ('ど' / 'らば')
    / 'ため'
    / 'とも'
    / 'で' ('しょう' / 'す')
    / 'の' ('は' / 'を')
    / 'に' ('は' / 'も' / 'つれて')
    / 'か' ('も' / 'の' / 'に' / 'を')

PSURUc = [なとか] // 赤い

PNAIw = 
  / 'ない'
  / 'なく'
  / 'なかった'
  / 'なければ'

PVERBw = 
  / 'し' _PVERBI // VS5 VS, 探します 行動します
  / 'し' _PVERBT // VS5,VS 探して 行動して
  / 'し' PNAIw // VS 行動しない 探さない  
  / 'しよう' // VS
  / 'せず' // VS 行動せず  探さず
  / 'する' / 'すれば'  // VS
  / 'せよ' // VS, VS5 
  / 'い' _PVERBT // VK5, VW5
  / 'っ' _PVERBT // VT5,VR5,VW5
  / 'ん' _PVERBD // VN5,VM5,
  / 'か' _PVERBA // カ行
  / 'き' _PVERBI
  / 'く' !(PNAIw/'なる'/[すし]) _PVERBU   // 赤くとの衝突
  / 'け' _PVERBE
  / 'こ' _PVERBO
  / 'さ' _PVERBA
  / 'す' _PVERBU
  / 'せ' _PVERBE
  / 'そ' _PVERBO
  / 'た' _PVERBA  // た行
  / 'ち' _PVERBI
  / 'つ' _PVERBU
  / 'て' _PVERBE
  / 'と' _PVERBO
  / 'な' _PVERBA  // な行
  / 'に' _PVERBI
  / 'ぬ' _PVERBU
  / 'ね' _PVERBE
  / 'の' _PVERBO
  / 'ま' _PVERBA
  / 'み' _PVERBI
  / 'む' _PVERBU
  / 'め' _PVERBE
  / 'も' _PVERBO
  / 'ら' _PVERBA
  / 'り' _PVERBI
  / 'る' _PVERBU
  / 'れ' _PVERBE
  / 'ろ' _PVERBO
  / 'わ' _PVERBA
  / 'う' _PVERBU
  / 'え' _PVERBE
  / 'お' _PVERBO
  / 'ば' _PVERBA
  / 'び' _PVERBI
  / 'ぶ' _PVERBU
  / 'べ' _PVERBE
  / 'ぼ' _PVERBO

PVERB1w = PVERB1c _PVERB1w
PVERB1c = [えきぎけげせじてめびべりれね]
_PVERB1w =  // 高めた 
  / 'る'  
  / 'た' // 
  / 'て'
  / 'られ'
  / PNAIw
  / 'ま' // せん

_PVERBA = PNAIw / 'れ' / 'さ' / 'ず'
_PVERBI = 'ま' / 'やす' / 'にく' / 'つつ' / K
_PVERBT = 'た' / 'て'
_PVERBD = 'だ' / 'で'
_PVERBU = K / PSURUc / PSURUw / EOF
_PVERBE = 'ば' / 'と' / [！!] / EOF
_PVERBO = 'う'

PKA5w = //書く
  / 'か' _PVERBA
  / 'き' _PVERBI
  / 'い' _PVERBT
  / 'く' !(PNAIw/'なる'/[すし]) _PVERBU
  / 'け' _PVERBE
  / 'こ' _PVERBO

PSA5w = //探す
  / 'さ' _PVERBA
  / 'し' _PVERBI
  / 'し' _PVERBT
  / 'す' _PVERBU
  / 'せ' _PVERBE
  / 'そ' _PVERBO

PTA5w = //勝つ
  / 'た' _PVERBA
  / 'ち' _PVERBI
  / 'っ' _PVERBT
  / 'つ' _PVERBU
  / 'て' _PVERBE
  / 'と' _PVERBO

PNA5w = //死ぬ
  / 'な' _PVERBA
  / 'に' _PVERBI
  / 'ん' _PVERBD
  / 'ぬ' _PVERBU
  / 'ね' _PVERBE
  / 'の' _PVERBO

PMA5w = //読む
  / 'ま' _PVERBA
  / 'み' _PVERBI
  / 'ん' _PVERBD
  / 'む' _PVERBU
  / 'め' _PVERBE
  / 'も' _PVERBO

PRA5w = //切る
  / 'ら' _PVERBA
  / 'り' _PVERBI
  / 'っ' _PVERBT
  / 'る' _PVERBU
  / 'れ' _PVERBE
  / 'ろ' _PVERBO

PWA5w = //笑う
  / 'わ' _PVERBA
  / 'い' _PVERBI
  / 'っ' _PVERBT
  / 'う' _PVERBU
  / 'え' _PVERBE
  / 'お' _PVERBO

PGA5w = //防ぐ
  / 'が' _PVERBA
  / 'ぎ' _PVERBI
  / 'い' _PVERBD
  / 'ぐ' _PVERBU
  / 'げ' _PVERBE
  / 'ご' _PVERBO

PBA5w = //遊ぶ
  / 'ば' _PVERBA
  / 'び' _PVERBI
  / 'ん' _PVERBD
  / 'ぶ' _PVERBU
  / 'べ' _PVERBE
  / 'ぼ' _PVERBO

Unknown = {
    (!K .)+
    #err
}

/* _Adjective */

_Adjective =
    / {^ '過ぎ' / 'すぎ' #Verb1 } _Verb1
    / {^ 'さ' !'れ' #Noun } _Noun?
    / {^ 'そう' #Noun } _Noun? 
    / {^ 'め' #Noun } _Verb1
    / _MoodNA 

/* _Noun */

_Noun = // もの
  / {^ 'な' #Adj}
  / {^ PNOUNw #Argument }
  / {^ PNOUNc #Argument }

_NounVerb = // 例. 行動する 論じる
    / {^ 'する' #Do } _VerbU
    / {^ [すず] #Do } ('れ' _VerbE / _Should) 
    / {^ [しじ] #Do } (_VerbA / 'よ' _VerbO / _VerbIX / _VerbI / !'か' _Noun?)
    / {^ 'され' #Been } _Verb1 
    / {^ 'させ' #Make } _Verb1
    / {^ 'せよ' #Command } _Base?
    / {^ 'せず' / 'せぬ' / 'できず' #Noun } _Noun?
    / {^ 'でき' #Can } _Verb1 

_Verb1 = // 過ぎ　述べ　視　でき
    / {^ 'る' #Verb } _VerbU  // 
    / {^ 'れ' #Verb } _VerbE  // ば
    / {^ 'られ' #Been } _Verb1 // 述べられています
    / {^ 'よ' #Verb } _VerbO  // う
    / _VerbA  // ない
    / _VerbIX // て
    / _VerbI　// て
    / {^ #Noun } _Noun?

_Verb = 
    / {^ 'する' #VS } _VerbU  // サ変
    / {^ 'す' #VS } ('れ' _VerbE / _Should) 
    / {^ 'しよ' #VS } _VerbO
    / {^ 'し' #VS } _VerbA
    / {^ 'し' #VSx } (_VerbIX / _VerbI / !'か' _Noun?)
    / {^ 'さ' &'れ' #VS } {'れ' #Been } _Verb1 
    / {^ 'さ' &'せ' #VS } {'せ' #Make } _Verb1
    / {^ 'せよ' CMD? #Command } _Base?
    / {^ 'せず' / 'せぬ' #Noun } _Noun?
    / {^ 'でき' #Can } _Verb1 
    / {^ 'い' #Vi5 } _VerbIX // #VK5@　#VW5@ #VG5@　た
    / {^ 'っ' #Vt5 } _VerbIX // #VT5@ #VR5@ #VW5@ た
    / {^ 'ん' #Vd5 } _VerbIXD // #VN5@ #VM5@ #VB5@
    / {^ 'か' #VK5 } _VerbA // 書
    / {^ 'き' #VK5 } _VerbI
    / {^ 'き' #Noun } _Noun?
    / {^ 'く' #VK5 } _VerbU
    / {^ 'け' #VK5 } _VerbE
    / {^ 'こ' #VK5 } _VerbO
    / {^ 'さ' #VS5 } _VerbA  // ない
    / {^ 'し' #Noun } _Noun?
    / {^ 'す' #VS5 } _VerbU  // 達すと
    / {^ 'せ' #VS5 } _VerbE  // ば
    / {^ 'そ' #VS5 } _VerbO  // う
    / {^ 'た' #VT5 } _VerbA  // ない
    / {^ 'ち' #VT5 } _VerbI   // ながら
    / {^ 'ち' #VT5 } _Noun?
    / {^ 'つ' #VT5 } _VerbU  // 
    / {^ 'て' #VT5 } _VerbE  // ば
    / {^ 'と' #VT5 } _VerbO  // う
    / {^ 'な' #VN5 } _VerbA  // ない
    / {^ 'に' #VN5 } _VerbI // ながら
    / {^ 'に' #Noun } _Noun?
    / {^ 'ぬ' #VN5 } _VerbU  // 
    / {^ 'ね' #VN5 } _VerbE  // ば
    / {^ 'の' #VN5 } _VerbO  // う
    / {^ 'ま' #VM5 } _VerbA  // ない
    / {^ 'み' #VM5 } _VerbI // ながら
    / {^ 'み' #Noun } _Noun?
    / {^ 'む' #VM5 } _VerbU  // 
    / {^ 'め' #VM5 } _VerbE  // ば
    / {^ 'も' #VM5 } _VerbO  // う
    / {^ 'ら' #VR5 } _VerbA  // ない
    / {^ 'り' #VR5 } _VerbI  // ながら
    / {^ 'り' #Noun } _Noun?
    / {^ 'る' #VR5 } _VerbU  // 
    / {^ 'れ' #VR5 } _VerbE  // ば
    / {^ 'ろ' #VR5 } _VerbO  // う
    / {^ 'わ' #VW5 } _VerbA  // ない
    / {^ 'い' #Noun } _Noun?
    / {^ 'う' #VW5 } _VerbU  // 
    / {^ 'え' #VW5 } _VerbE  // ば
    / {^ 'お' #VW5 } _VerbO  // う
    / {^ 'が' #VG5 } _VerbA  // ない
    / {^ 'ぎ' #VG5 } _VerbI  // ながら
    / {^ 'ぎ' #Noun } _Noun?
    / {^ 'ぐ' #VG5 } _VerbU  // 
    / {^ 'げ' #VG5 } _VerbE  // ば
    / {^ 'ご' #VG5 } _VerbO  // う
    / {^ 'ば' #VB5 } _VerbA  // ない
    / {^ 'び' #VB5 } _VerbI 　// ながら
    / {^ 'び' #Noun } _Noun?   //　+ 名詞
    / {^ 'ぶ' #VB5 } _VerbU  // 
    / {^ 'べ' #VB5 } _VerbE  // ば
    / {^ 'ぼ' #VB5 } _VerbO  // う


_VerbKA5 = // 書
    / {^ 'か' #Verb } _VerbA
    / {^ 'き' #Verb } _VerbI
    / {^ 'き' #Noun } _Noun?
    / {^ 'い' #Verb } _VerbIX
    / {^ 'く' #Verb } _VerbU
    / {^ 'け' #Verb } _VerbE
    / {^ 'こ' #Verb } _VerbO

_VerbSA5 = // 探 出
    / {^ 'さ' #Verb } _VerbA  // ない
    / {^ 'し' #Verb } _VerbIX // て
    / {^ 'し' #Verb } _VerbI   // ながら
    / {^ 'し' #Noun } _Noun?
    / {^ 'す' 'る'? #Verb } _VerbU  // 達すると
    / {^ 'せ' #Verb } _VerbE  // ば
    / {^ 'そ' #Verb } _VerbO  // う

_VerbTA5 = // 勝
    / {^ 'た' #Verb } _VerbA  // ない
    / {^ 'ち' #Verb } _VerbI   // ながら
    / {^ 'ち' #Noun } _Noun?
    / {^ 'っ' #Verb } _VerbIX // て
    / {^ 'つ' #Verb } _VerbU  // 
    / {^ 'て' #Verb } _VerbE  // ば
    / {^ 'と' #Verb } _VerbO  // う

_VerbNA5 = // 死
    / {^ 'な' #Verb } _VerbA  // ない
    / {^ 'に' #Verb } _VerbI // ながら
    / {^ 'に' #Noun } _Noun?
    / {^ 'ん' #Verb } _VerbIXD // て
    / {^ 'ぬ' #Verb } _VerbU  // 
    / {^ 'ね' #Verb } _VerbE  // ば
    / {^ 'の' #Verb } _VerbO  // う

_VerbMA5 = // 読
    / {^ 'ま' #Verb } _VerbA  // ない
    / {^ 'み' #Verb } _VerbI // ながら
    / {^ 'み' #Noun } _Noun?
    / {^ 'ん' #Verb } _VerbIXD // て
    / {^ 'む' #Verb } _VerbU  // 
    / {^ 'め' #Verb } _VerbE  // ば
    / {^ 'も' #Verb } _VerbO  // う

_VerbRA5 = // 切
    / {^ 'ら' #Verb } _VerbA  // ない
    / {^ 'り' #Verb } _VerbI  // ながら
    / {^ 'り' #Noun } _Noun?
    / {^ 'っ' #Verb } _VerbIX // て
    / {^ 'る' #Verb } _VerbU  // 
    / {^ 'れ' #Verb } _VerbE  // ば
    / {^ 'ろ' #Verb } _VerbO  // う

_VerbWA5 = // 笑
    / {^ 'わ' #Verb } _VerbA  // ない
    / {^ 'い' #Verb } _VerbI  // ます
    / {^ 'い' #Noun } _Noun?
    / {^ 'っ' #Verb } _VerbIX // て
    / {^ 'う' #Verb } _VerbU  // 
    / {^ 'え' #Verb } _VerbE  // ば
    / {^ 'お' #Verb } _VerbO  // う

_VerbGA5 = // 防
    / {^ 'が' #Verb } _VerbA  // ない
    / {^ 'ぎ' #Verb } _VerbI  // ながら
    / {^ 'ぎ' #Noun } _Noun?
    / {^ 'い' #Verb } _VerbIXD // て
    / {^ 'ぐ' #Verb } _VerbU  // 
    / {^ 'げ' #Verb } _VerbE  // ば
    / {^ 'ご' #Verb } _VerbO  // う

_VerbBA5 = // 遊
    / {^ 'ば' #Verb } _VerbA  // ない
    / {^ 'び' #Verb } _VerbI 　// ながら
    / {^ 'び' #Noun } _Noun?   //　+ 名詞
    / {^ 'ん' #Verb } _VerbIXD // て
    / {^ 'ぶ' #Verb } _VerbU  // 
    / {^ 'べ' #Verb } _VerbE  // ば
    / {^ 'ぼ' #Verb } _VerbO  // う

_VerbA = // 書か+ない   探さ+ない
    / {^ 'な' #Not } _MoodNA
    / {^ 'ず' #Noun } _Noun?
    / {^ 'さ'? 'せ' 'られ'? #Make } _Verb1
    / {^ 'れ' #Been } _Verb1 

_VerbI = // 書き+つつ  探し+つつ　
    / &'ま' _VerbIPolite
    / {^ 'た' #Want } _MoodNA   // 書きたい
    / {^ ('やす' / 'よ' ) #Easy } _MoodNA  // 書きやすい
    / {^ ('にく' / 'がた' / 'づら') #Hard } _MoodNA //書きがたい
    / {^ 'がち' #Noun } _Noun? //休みがちだ
    / {^ ('つつ' / 'ながら') #While }
    / TEN

_VerbIPolite =
    / {^ ('ます' / 'ましょう') #Base } _Base?
    / {^ 'ません' #Not } _Base?
    / {^ 'ましたら' #If }
    / {^ 'ました' #Past } _Past?
    / {^ 'ましても' #EvenIf }
    / {^ 'まして' #And }

_VerbIX = // 書い+て   探し+て　
    / {^ 'た' NH #Want } _MoodNA   // 特例. 記録したい
    / {^ 'た' #Past } _Past?
    / {^ 'てから' #After } _Noun?
    / {^ 'て' (&'も' NH .) #EvenIf }
    / {^ 'て' #Noun } _Noun
    / {^ 'て' #And } _TryMood?

_VerbIXD = // 遊んだ   遊んで
    / {^ 'だ' #Past } _Past?
    / {^ 'でから' #After } _Noun?
    / {^ 'で' (&'も' NH .) #EvenIf }
    / {^ 'で' #Noun } _Noun
    / {^ 'で' IGNORED? #And } _TryMood?

_VerbU = _Base? // 書く　探す

_VerbE = // 書け+ば  探せ+ば
    / {^ 'ば' #If }
    / {^ #Able} _Verb1
    / {^ #Command }

_VerbO = // 書こ+う　探そ+う
    / {^ 'う' #Will } _Base?

_Should = 
    {^ ('べき' / 'べし' / 'はず' ) #Should } _Noun?

_MoodNA = //な
    / {^ 'い' #Base } ({^ 'で' #And} / _Base)?
    / {^ 'かった' #Past } _Past?
    / {^ 'ければ' #If }
    / {^ 'くな' #Not } _MoodNA
    / {^ 'くな' #Become } _VerbRA5
    / {^ 'くて' #Noun } _Noun // なくてはいけません
    / {^ 'くすべき' #Should } _Noun? // なくすべき
    / 'く' _NounVerb  // なく
    / {^ 'く' &[はもを] #Noun } _NounOf // 大きくはない、大きくはありません
    / {^ 'くに' #Adverb }
    / {^ 'くても' #EvenIf }
    / {^ 'く' 'て'? #And }
    / {^ 'さ' #Noun } _Noun?

_Base =
  / {^ 'かも' [知し] 'れ' ('ない' / 'ません')? #May } _Base?
  / {^ 'しかありません' #Only }
  / {^ 'けれども' #Though } // けれども
  / {^ 'らし' #Sound } _MoodNA   // するらしい
  / {^ 'のみ'/ 'だけ' #OnlyThat } _Noun?
  / {^ 'ため' / '為' / 'から' #Because } _Noun?

_Past = // した
    / {^ 'り' #Noun } (_NounVerb / _Noun)
    / {^ 'り' 'し'? #Then }
    / {^ 'な'? 'ら' 'ば'? #If } 
    / _Base

NH = ''

_TryMood =
    / {^ 'み' #Try } _Verb1 // 笑ってみる
    // / {^ 'い' #Going } _VerbKA5  // 
    // / {^ 'い' #Now } _Verb1    // 笑っている, 増加していく
    // / {^ 'くれ' #Given } _Verb1    // 笑ってくれる
    // / {^ 'お' #Done } _VerbKA5    // 笑っておく
    // / _Exist                 // 笑ってある 
    // / Come                  // 笑ってくる
    // / {^ &HIRA #Now } _Verb1    // 笑ってる (口語)

/* Expression */




/* character */

SPC = [ ]
EOF = !.
EOS = [。．.\n]? EOF
TEN = [、，,：　 \n]
KANJI = [㐀-䶵一-龠々〇〻ーご]
KATA = [ァ-ヶー・]
HIRA = [ぁ-ん]
NONHIRA = [㐀-䶵一-龠々〇〻ーごァ-ヶー・] 
NONHIRA2 = (!HIRA !TEN ![()（）0-9A-Za-z] .)
ALPHA = [a-zA-Zａ-ｚＡ-Ｚ]
NUM = [\.,0-9０-９一二三四五六七八九十百千万億兆京垓秭穰溝澗正載極]
SYMBOL = [^＾~〜\-ー_＿!！?？#＃$＄&＆=＝@＠*＊+＋/・]
CMD = [！!]


